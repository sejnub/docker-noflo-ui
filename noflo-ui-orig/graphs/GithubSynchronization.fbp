INPORT=Dispatch.IN:IN
INPORT=Token.IN:TOKEN
OUTPORT=MergeContext.OUT:CONTEXT
OUTPORT=Changed.OUT:CHANGED
OUTPORT=Errors.OUT:ERROR

'prepare,synchronize,pull' -> ROUTES Dispatch(routers/GroupRouter)
Dispatch MISSED -> IN ShowErrors(core/Output)

# Preparation for sync: check local and remote situation
'repo' -> KEY GetRepo(objects/GetObjectKey)
Dispatch OUT[0] -> IN GetRepo
Token(core/Repeat) OUT -> TOKEN GetReference(github/GetReference)
GetRepo OUT -> REPOSITORY GetReference
GetReference REFERENCE -> REFERENCE CheckSyncState(ui/CheckSyncState)
'project' -> KEY GetProject(objects/GetObjectKey)
GetRepo OBJECT -> IN GetProject OUT -> PROJECT CheckSyncState
Token OUT -> TOKEN CheckSyncState
GetReference ERROR -> IN Errors(core/Merge)
CheckSyncState ERROR -> IN Errors

# Different synchronization states
CheckSyncState NOOP -> IN MergeOps(core/Merge)
CheckSyncState LOCAL -> IN MergeOps
CheckSyncState REMOTE -> IN MergeOps
CheckSyncState BOTH -> IN MergeOps
'syncOperation' -> KEY OpsToContext(ui/SetToContext)
MergeOps OUT -> VALUE OpsToContext
MergeOps OUT -> START CreateCtx(ui/CreateEmptyContext) OUT -> CONTEXT OpsToContext
OpsToContext CONTEXT -> IN MergeContext(core/Merge)

# Actual synchronization operation(s)
Dispatch OUT[1] -> IN DoSync(core/Repeat)

# First pull changes in
DoSync OUT -> IN PreparePulls(ui/PreparePulls)
Token OUT -> TOKEN GetBlob(github/GetBlob)
PreparePulls OUT -> OPERATION BlobToEntry(ui/BlobToEntry)
PreparePulls REPOSITORY -> REPOSITORY GetBlob
PreparePulls SHA -> SHA GetBlob
GetBlob OUT -> BLOB BlobToEntry
BlobToEntry COMPONENT -> IN UpdatedComponent(core/Merge)
BlobToEntry GRAPH -> IN UpdatedGraph(core/Merge)
BlobToEntry SPEC -> IN UpdatedSpec(core/Merge)
BlobToEntry ERROR -> IN ShowErrors
GetBlob ERROR -> IN Errors

# Then push local changes
DoSync OUT -> IN PreparePush(ui/PreparePush)
PreparePush OUT -> IN UpdateFromTree(ui/UpdateFromTree)
PreparePush BASETREE -> BASE CreateTree(github/CreateTree)
Token OUT -> TOKEN CreateTree
PreparePush TREE -> TREE CreateTree
PreparePush REPOSITORY -> REPOSITORY CreateTree
CreateTree OUT -> TREE UpdateFromTree
PreparePush REPOSITORY -> REPOSITORY UpdateFromTree
Token OUT -> TOKEN UpdateFromTree
'sha' -> KEY GetTreeSha(objects/GetObjectKey)
CreateTree OUT -> IN GetTreeSha
GetTreeSha OUT -> TREE CreateCommit(github/CreateCommit)
Token OUT -> TOKEN CreateCommit
PreparePush MESSAGE -> MESSAGE CreateCommit
PreparePush PARENTCOMMITS -> PARENTS CreateCommit
PreparePush REPOSITORY -> REPOSITORY CreateCommit

CreateCommit OUT -> COMMIT UpdateReference(github/UpdateReference)
PreparePush REF -> REFERENCE UpdateReference
Token OUT -> TOKEN UpdateReference
PreparePush REPOSITORY -> REPOSITORY UpdateReference
UpdateReference REFERENCE -> IN ShowErrors

UpdateFromTree COMPONENT -> IN UpdatedComponent
UpdateFromTree GRAPH -> IN UpdatedGraph
UpdateFromTree SPEC -> IN UpdatedSpec
UpdateFromTree ERROR -> IN ShowErrors
CreateTree ERROR -> IN Errors
CreateCommit ERROR -> IN Errors
UpdateReference ERROR -> IN Errors

# Grouping
'save:graph' -> GROUP SetGraphGroup(groups/Group)
UpdatedGraph OUT -> IN DiscGraph(core/DisconnectAfterPacket)
DiscGraph OUT -> IN SetGraphGroup OUT -> IN Changed(core/Merge)
'save:component' -> GROUP SetComponentGroup(groups/Group)
UpdatedComponent OUT -> IN DiscComponent(core/DisconnectAfterPacket)
DiscComponent OUT -> IN SetComponentGroup OUT -> IN Changed
'save:spec' -> GROUP SetSpecGroup(groups/Group)
UpdatedSpec OUT -> IN DiscSpec(core/DisconnectAfterPacket)
DiscSpec OUT -> IN SetSpecGroup OUT -> IN Changed
Changed OUT -> IN ShowErrors

# Pull-only sync, no decision needed
'repo' -> KEY GetPullRepo(objects/GetObjectKey)
Dispatch OUT[2] -> IN GetPullRepo
Token(core/Repeat) OUT -> TOKEN GetPullReference(github/GetReference)
GetPullRepo OUT -> REPOSITORY GetPullReference
GetPullReference REFERENCE -> REFERENCE CheckPullState(ui/CheckSyncState)
'project' -> KEY GetPullProject(objects/GetObjectKey)
GetPullRepo OBJECT -> IN GetPullProject OUT -> PROJECT CheckPullState
Token OUT -> TOKEN CheckPullState
GetPullReference ERROR -> IN Errors(core/Merge)
CheckPullState REMOTE -> IN DoSync
CheckPullState ERROR -> IN Errors
