INPORT=HoldDb.DATA:DB
INPORT=Dispatch.IN:IN
OUTPORT=MergeContext.OUT:OUT

'component,graph,project,runtime,spec' -> ROUTES Dispatch(routers/GroupRouter)

# Create transaction
HoldDb(core/Kick) OUT -> DB WriteTransaction(indexeddb/BeginTransaction)
'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE WriteTransaction
StoreName(core/Merge) OUT -> STORES WriteTransaction
Dispatch ROUTE -> IN HoldDb
Dispatch ROUTE -> IN HoldMode
Dispatch MISSED -> IN ShowErrors(core/Output)

# Component-specific handling
'components' -> DATA ComponentStore(core/Kick) OUT -> IN StoreName
Dispatch OUT[0] -> IN SplitComponent(core/Split) OUT -> IN ComponentStore 
SplitComponent OUT -> IN EnsureId(ui/EnsureId)

# Graph-specific handling
'graphs' -> DATA GraphStore(core/Kick) OUT -> IN StoreName
Dispatch OUT[1] -> IN SplitGraph(core/Split) OUT -> IN GraphStore
'toJSON' -> METHOD ConvertGraph(objects/CallMethod)
SplitGraph OUT -> IN ConvertGraph OUT -> IN EnsureId

# Project-specific handling
'projects' -> DATA ProjectStore(core/Kick) OUT -> IN StoreName
Dispatch OUT[2] -> IN SplitProject(core/Split) OUT -> IN ProjectStore 
SplitProject OUT -> IN EnsureId

# Runtime-specific handling
'runtimes' -> DATA RuntimeStore(core/Kick) OUT -> IN StoreName
Dispatch OUT[3] -> IN SplitRuntime(core/Split) OUT -> IN RuntimeStore 
SplitRuntime OUT -> IN EnsureId

# FbpSpec-specific handling
'specs' -> DATA SpecStore(core/Kick) OUT -> IN StoreName
Dispatch OUT[4] -> IN SplitSpec(core/Split) OUT -> IN SpecStore 
SplitSpec OUT -> IN EnsureId

# Saving
StoreName OUT -> NAME Store(indexeddb/GetStore)
WriteTransaction TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
EnsureId OUT -> VALUE Put
'state=ok' -> PROPERTY SetOk(objects/SetProperty)
Put KEY -> START CreateOk(objects/CreateObject) OUT -> IN SetOk OUT -> IN MergeContext

# Error handling
Put ERROR -> ERROR ErrorToCtx(ui/ErrorToContext) OUT -> IN MergeContext(core/Merge)
